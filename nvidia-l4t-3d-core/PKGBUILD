pkgname=nvidia-l4t-3d-core
pkgver=32.6.1_20210916211029
pkgrel=1
pkgdesc='NVIDIA GL EGL Package'
arch=(aarch64)
url='https://repo.download.nvidia.com/jetson/'
license=('custom')
depends=('nvidia-l4t-firmware=32.6.1_20210916211029' 'nvidia-l4t-init=32.6.1_20210916211029' 'nvidia-l4t-libvulkan=32.6.1_20210916211029' 'nvidia-l4t-wayland=32.6.1_20210916211029' 'nvidia-l4t-x11=32.6.1_20210916211029' )
source=('https://repo.download.nvidia.com/jetson/t210/pool/main/n/nvidia-l4t-3d-core/nvidia-l4t-3d-core_32.6.1-20210916211029_arm64.deb')
sha256sums=('96f7eaa8832fbe68d585e5dfed9b1c10db738e53289e8a06c6b4bd5535ea120f')

prepare() {
  mkdir -p root
  tar xvf data.tar* -C root
}

package() {
  cp -a root/. $pkgdir/

  # On Arch, /lib is symlink to /usr/lib
  if test -d $pkgdir/lib; then
    cp -a $pkgdir/lib/. $pkgdir/usr/lib/
    rm -rf $pkgdir/lib
  fi

  # On Arch, sbin is a symlink to bin
  if test -d $pkgdir/usr/sbin; then
    cp -a $pkgdir/usr/sbin/. $pkgdir/usr/bin/
    rm -rf $pkgdir/usr/sbin
  fi

  # Arch on ARM doesn't use multilib
  if test -d $pkgdir/lib/aarch64-linux-gnu; then
    cp -a $pkgdir/lib/aarch64-linux-gnu/. $pkgdir/lib/
    rm -rf $pkgdir/lib/aarch64-linux-gnu
  fi
  if test -d $pkgdir/usr/lib/aarch64-linux-gnu; then
    cp -a $pkgdir/usr/lib/aarch64-linux-gnu/. $pkgdir/usr/lib/
    rm -rf $pkgdir/usr/lib/aarch64-linux-gnu
  fi

  # Fix up a link that has multiarch path in it
  rm $pkgdir/usr/share/glvnd/egl_vendor.d/10_nvidia.json
  ln -s ../../../lib/tegra-egl/nvidia.json $pkgdir/usr/share/glvnd/egl_vendor.d/10_nvidia.json
  rm $pkgdir/etc/vulkan/icd.d/nvidia_icd.json
  ln -s /usr/lib/tegra/nvidia_icd.json $pkgdir/etc/vulkan/icd.d/nvidia_icd.json
}
