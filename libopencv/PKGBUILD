pkgname=libopencv
pkgver=4.1.1_2_gd5a58aa75
pkgrel=1
pkgdesc='Open Computer Vision Library'
arch=(aarch64)
url='https://repo.download.nvidia.com/jetson/'
license=('custom')
depends=()
source=('https://repo.download.nvidia.com/jetson/common/pool/main/libo/libopencv/libopencv_4.1.1-2-gd5a58aa75_arm64.deb')
sha256sums=('0edfb3ece5cff97d452ac55ca9caf4e5e40f083eae91dbf804bfc99096447aa6')

prepare() {
  mkdir -p root
  tar xvf data.tar* -C root
}

package() {
  cp -a root/. $pkgdir/

  # On Arch, /lib is symlink to /usr/lib
  if test -d $pkgdir/lib; then
    cp -a $pkgdir/lib/. $pkgdir/usr/lib/
    rm -rf $pkgdir/lib
  fi

  # On Arch, sbin is a symlink to bin
  if test -d $pkgdir/usr/sbin; then
    cp -a $pkgdir/usr/sbin/. $pkgdir/usr/bin/
    rm -rf $pkgdir/usr/sbin
  fi

  # Arch on ARM doesn't use multilib
  if test -d $pkgdir/lib/aarch64-linux-gnu; then
    cp -a $pkgdir/lib/aarch64-linux-gnu/. $pkgdir/lib/
    rm -rf $pkgdir/lib/aarch64-linux-gnu
  fi
  if test -d $pkgdir/usr/lib/aarch64-linux-gnu; then
    cp -a $pkgdir/usr/lib/aarch64-linux-gnu/. $pkgdir/usr/lib/
    rm -rf $pkgdir/usr/lib/aarch64-linux-gnu
  fi
}
