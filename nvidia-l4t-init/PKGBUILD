pkgname=nvidia-l4t-init
pkgver=32.6.1_20210916211029
pkgrel=1
pkgdesc='NVIDIA Init debian package'
arch=(aarch64)
url='https://repo.download.nvidia.com/jetson/'
license=('custom')
depends=('bridge-utils')
source=('https://repo.download.nvidia.com/jetson/t210/pool/main/n/nvidia-l4t-init/nvidia-l4t-init_32.6.1-20210916211029_arm64.deb')
sha256sums=('bee82489940152358039b5b7123815620447415121f03843f711a8f4158b23b4')

prepare() {
  mkdir -p root
  tar xvf data.tar* -C root
}

package() {
  cp -a root/. $pkgdir/

  rm $pkgdir/etc/systemd/sleep.conf

  # On Arch, /lib is symlink to /usr/lib
  if test -d $pkgdir/lib; then
    cp -a $pkgdir/lib/. $pkgdir/usr/lib/
    rm -rf $pkgdir/lib
  fi

  # On Arch, sbin is a symlink to bin
  if test -d $pkgdir/usr/sbin; then
    cp -a $pkgdir/usr/sbin/. $pkgdir/usr/bin/
    rm -rf $pkgdir/usr/sbin
  fi

  # Arch on ARM doesn't use multilib
  if test -d $pkgdir/lib/aarch64-linux-gnu; then
    cp -a $pkgdir/lib/aarch64-linux-gnu/. $pkgdir/lib/
    rm -rf $pkgdir/lib/aarch64-linux-gnu
  fi
  if test -d $pkgdir/usr/lib/aarch64-linux-gnu; then
    cp -a $pkgdir/usr/lib/aarch64-linux-gnu/. $pkgdir/usr/lib/
    rm -rf $pkgdir/usr/lib/aarch64-linux-gnu
  fi
}
